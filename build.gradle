buildscript {
}


allprojects {

    group 'com.jean'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {

    ext {
        springBootVersion = '1.5.8.RELEASE'
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'


    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                //若是war包，就写components.web,若是jar包，就写components.java
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url = "http://127.0.0.1:8081/repository/maven-snapshots/"
                } else {
                    url = "http://127.0.0.1:8081/repository/maven-releases/"
                }

                credentials {
                    username 'admin'
                    password ''
                }
            }
        }
    }
}